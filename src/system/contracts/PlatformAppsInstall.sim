contract PlatformAppsInstall {
    data {}
    func appendOnce(ar array, menu, page, item string) array{
        if !Contains(menu, page) {
            ar = Append(ar, item)
        }
        return ar
    }

    func getAdminMenu() string {
        var items array currentMenu string
        currentMenu = Str($developer_menu["value"])

        items = appendOnce(items, currentMenu, "@1apps_list", `MenuItem(Title:$@1applications$, Page:@1apps_list, Icon:"icon-folder")`)
        items = appendOnce(items, currentMenu, "@1params_list", `MenuItem(Title:$@1ecosystem_parameters$, Page:@1params_list, Icon:"icon-settings")`)
        items = appendOnce(items, currentMenu, "@1menus_list", `MenuItem(Title:$@1menu$, Page:@1menus_list, Icon:"icon-list")`)
        items = appendOnce(items, currentMenu, "@1section_list", `MenuItem(Title:$@1sections$, Page:@1section_list, Icon:"icon-frame")`)
        items = appendOnce(items, currentMenu, "@1app_langres", `MenuItem(Title:$@1lang_res$, Page:@1app_langres, Icon:"icon-globe")`)
        items = appendOnce(items, currentMenu, "@1developer_index", `MenuItem(Title:$@1dashboard$, Page:@1developer_index, Icon:"icon-wrench")`)
        
        return Join(items array, "\n")
    }

    func getDefaultMenu() string {
        var items array currentMenu string
        currentMenu = Str($default_menu["value"])

        items = appendOnce(items, currentMenu, "@1profile_view", `MenuItem(Title:$@1profile$, Page:@1profile_view, Icon:"icon-wallet")`)
        items = appendOnce(items, currentMenu, "@1notifications_list", `MenuItem(Title:$@1notifications$, Page:@1notifications_list, Icon:"icon-flag")`)
        items = appendOnce(items, currentMenu, "@1members_list", `MenuItem(Title:$@1ecosystem_members$, Page:@1members_list, Icon:"fa icon-people")`)
        items = appendOnce(items, currentMenu, "@1ecosystems_list", `MenuItem(Title:$@1ecosystems$, Page:@1ecosystems_list, Icon:"fa fa-list-ul")`)
        if !(Contains(currentMenu, "@1roles_list") || Contains(currentMenu, "@1voting_list")){
            items = Append(items, `MenuGroup(Title:$@1admin$, Icon:"icon-arrow-right"){`)
            items = Append(items, `    MenuItem(Title:$@1roles$, Page:@1roles_list, Icon:"fa fa-list-ul")`)
            items = Append(items, `    MenuItem(Title:$@1votings$, Page:@1voting_list, Icon:"fa fa-edit")`)
            items = Append(items, `}`)
        }

        return Join(items array, "\n")
    }

    conditions {
        $developer_menu = DBFind("@1menu").Columns("id,value").Where({name: "developer_menu", ecosystem: $ecosystem_id}).Row()
        $default_menu = DBFind("@1menu").Columns("id,value").Where({name: "default_menu", ecosystem: $ecosystem_id}).Row()

        if !$developer_menu {
            warning LangRes("@1developer_menu_not_found", "en")
        }
        if !$default_menu {
            warning LangRes("@1default_menu_not_found", "en")
        }
    }

    action {
        var val_developer_menu string
        val_developer_menu = getAdminMenu()
        EditMenu("Id,Value",Int($developer_menu["id"]), val_developer_menu)

        var val_default_menu string
        val_default_menu = getDefaultMenu()
        EditMenu("Id,Value", Int($default_menu["id"]), val_default_menu)
    }
}