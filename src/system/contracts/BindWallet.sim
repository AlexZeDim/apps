contract BindWallet {
	data {
		Id  int
        NotificId int "optional"
		WalletId string "optional"
	}
	conditions {
		$cur = DBFind("@1contracts").Columns("id,name,conditions,wallet_id").Where({id: $Id, ecosystem: $ecosystem_id}).Row()
		if !$cur {
			error Sprintf(LangRes("@1contract_x_not_exist", "en"), $Id)
		}
		if Int($cur["wallet_id"]) != 0 {
			error Sprintf(LangRes("@1contract_x_binded_already", "en"), $Id)
		}
		if $WalletId != "" && AddressToId($WalletId) == 0 {
			error LangRes("@1wallet_not_found", "en")
		}
        if $NotificId != 0 {
            $request = DBFind("@1notifications").Columns("id,closed,page_params->contract_id").Where({id: $NotificId, ecosystem: $ecosystem_id}).Row()
            if Int($request["closed"]) == 1 {
                warning LangRes("@1wrong_type", "en")
            }
        }
	}
	action {
		if $WalletId == "" || AddressToId($WalletId) == $key_id {
			BndWallet($Id, $ecosystem_id)
            if $NotificId != 0 {        
                var close_params map
                close_params["notific_id"] = $NotificId
                CallContract("NotificationsClose", close_params)
            }
		} else {
			if GetContractByName("NotificationsSend") == 0 {
				error LangRes("@1notifications_install_needed", "en")
			} else {
				var notific_params custom_params map
                custom_params["contract_id"] = $Id
                custom_params["contract_name"] = $cur["name"]
				notific_params["member_id"] = AddressToId($WalletId)
				notific_params["sender"] = 1
				notific_params["icon_name"] = "icon icon-link"
				notific_params["text_header"] = LangRes("@1request_contract_binding", "en")
				notific_params["text_body"] = LangRes("@1details_view", "en")
				notific_params["page_name"] = "contract_bind_request"
				notific_params["params_map"] = custom_params
				CallContract("NotificationsSend", notific_params)
			}
		}
	}
}