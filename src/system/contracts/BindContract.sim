contract BindContract {
	data {
		Id  int
        NotificId int "optional"
		WalletId string "optional"
	}
	conditions {
		$cur = DBFind("@1contracts").Columns("id,name,conditions,wallet_id").Where({id: $Id, ecosystem: $ecosystem_id}).Row()
		if !$cur {
			error Sprintf("Contract %d does not exist", $Id)
		}
		if Int($cur["wallet_id"]) != 0 {
			error Sprintf("The contract %d has been already binded", $Id)
		}
		if $WalletId != "" && AddressToId($WalletId) == 0 {
			error "There is no wallet with this ID"
		}
        if $NotificId != 0 {
            $request = DBFind("@1notifications").Columns("id,closed,page_params->contract_id").Where({id: $NotificId, ecosystem: $ecosystem_id}).Row()
            if Int($request["closed"]) == 1 {
                warning "Request already processed"
            }
        }
	}
	action {
		if $WalletId == "" || AddressToId($WalletId) == $key_id {
			DBUpdate("@1contracts", $Id, {"wallet_id": $key_id})
            if $NotificId != 0 {        
                var close_params map
                close_params["notific_id"] = $NotificId
                CallContract("NotificationsClose", close_params)
            }
		} else {
			if GetContractByName("NotificationsSend") == 0 {
				error "You should install application which contain contract for sending notifications first"
			} else {
				var notific_params custom_params map
                custom_params["contract_id"] = $Id
                custom_params["contract_name"] = $cur["name"]
				notific_params["member_id"] = AddressToId($WalletId)
				notific_params["sender"] = 1
				notific_params["icon_name"] = "icon icon-link"
				notific_params["text_header"] = "Contract binding request"
				notific_params["text_body"] = "View details"
				notific_params["page_name"] = "contract_bind_request"
				notific_params["params_map"] = custom_params
				CallContract("NotificationsSend", notific_params)
			}
		}
	}
	func rollback() {
		DBUpdate("@1contracts", $Id, {"wallet_id": 0})
	}

}