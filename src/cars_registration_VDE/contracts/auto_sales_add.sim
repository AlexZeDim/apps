contract auto_sales_add {
    data {
        auto_id int
        engine_number string
        chassis_number string
        isRandom int
        member_photo_dealer string "image"
    }

    conditions {}

    action {
		// if its a random buyer, get photo from the table
        if ($isRandom == 1){
            $photo_map = DBFind("random_buyer").Where("flag_selection=$", 1).Row()
            if ($photo_map["id"] == nil){
                warning "Photo not found"
            }
            $member_photo_dealer = string($photo_map["buyer_photo"]) 
        }

		// start recognizing
        var labels map
        labels = awsrek_getLabels($member_photo_dealer)

        var labels_keys array
        labels_keys = GetMapKeys(labels)

        var body_styles array
        body_styles = DBFind("car_classification")

        var i int
        var body_styles_len int
        body_styles_len = Len(body_styles)
        var labels_keys_len int
        labels_keys_len = Len(labels_keys)
        var found_body_styles array
        var counter int

        while (i < body_styles_len) {
            var k int
            var body_style map
            body_style = body_styles[i]
            while (k < labels_keys_len) {
                if (labels_keys[k] == body_style["body_style"]) {
                    found_body_styles[counter] = Sprintf("%s - %s", body_style["body_style"], Str(labels[body_style["body_style"]]))
                    counter = counter + 1
                    k = k + 1
                    break
                }
                k = k + 1
            }
            i = i + 1
        }

        i = Len(found_body_styles)
        while (i < 4) {
            found_body_styles[i] = ""
            i = i + 1
        }

		// get data after recognition
        $member = awsrek_getMemberIdByFace($member_photo_dealer)
        $member_id = $member["id"]
        $face_recognition_confidence = Float($member["confidence"])
        
		// search member
		$member_map = DBFind("recognition_members").Where("recognition_id=$", $member_id).Row()
		if ($member_map["id"] == nil) {
			warning "Member not recognized"
		}
		
		// search auto
		$auto_map = DBFind("registry_auto").Where("id=?", $auto_id).Row()
		if ($auto_map["id"] == nil) {
			warning "Auto not found"
		}
		
		// create a protocol
		DBInsert("registration_protocols", "member_id,member_name,member_photo,auto_id,auto_manufacturer,auto_model,engine_number,chassis_number,auto_photo,member_photo_dealer,timestamp member_photo_dealer_date,delete,status,face_recognition_confidence_double, body_type_1, body_type_2, body_type_3, body_type_4",
			$member_id, string($member_map["member_name"]), string($member_map["avatar"]),
			$auto_id, string($auto_map["manufacturer"]), string($auto_map["model"]), $engine_number, $chassis_number, string($auto_map["auto_photo"]),
			$member_photo_dealer, $time, 0, 0, $face_recognition_confidence, found_body_styles[0], found_body_styles[1], found_body_styles[2], found_body_styles[3])
    }
}