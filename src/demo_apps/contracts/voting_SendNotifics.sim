contract voting_SendNotifics {
    data {
        votingID int
    }

    conditions {
		$voting_id = DBFind(`voting_instances`).Where("id=$ and enddate > now()", $votingID).One("id")
        if ($voting_id == nil){
			warning "Voting has expired. Notifications can not be sent"
		}
	
		$voting_map = DBFind("voting_instances").Where("id=$ and startdate < now()", $votingID).Row()
		if ($voting_map["id"] == nil){
			warning "Voting has not yet begun. Try again later, please"
		}	
		
		if( Int($voting_map["creator_id"]) != $key_id ){
			warning "You are not the creator of the voting"
		}
		if( Int($voting_map["flag_notifics"]) == 1 ){
			warning "Notifications have already been sent"
		}
		
		$voting_name = string($voting_map["name"])
    }

    action {
		$ret_participants = DBFind("voting_participants").Where("voting_id=$ and decision=$", $votingID, 0)
		$i = 0
		while($i < Len($ret_participants)){
			$vals_participants = $ret_participants[$i]
			$recipient = Int($vals_participants["member_id"])
			
			var params string
			params = "notific_param=" + Str($votingID)
			
			Notifications_Single_Send("member_id,icon_name,text_header,text_body,page_name,params_val", 
								$recipient, "fa-check", "Voting", $voting_name, "voting_view", params)
			$i = $i + 1
		}
		
		DBUpdate(`voting_instances`, $votingID, `flag_notifics`, 1)
    }
}