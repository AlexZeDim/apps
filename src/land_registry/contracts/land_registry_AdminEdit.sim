contract land_registry_AdminEdit{
    data {
        LandId int
        Coords string // {"coords":[{"lat":float,"lng":float},],"area": float,"address": string}
        LandUse int
        BuildingsUseClass int
        // Area int
        // CoordsAddress string
        LandNature int
        LandLocalBody int
    }
    func conditions {
        $landRegistryRecord = DBFind("land_registry").Where("id=?", $LandId).Row()
        if !$landRegistryRecord{
            warning "Land Registry record not found"
        }
    }
    
    func log(field, value string){
        if value != $landRegistryRecord[field] {
            var name string
            name = DBFind("members").Where("id=?", $key_id).One("member_name")
            
            DBInsert("land_registry_editing", "editing_attribute,lend_object_id,new_attr_value,old_attr_value,person_id,person_name,timestamp date", field, $LandId, value, $landRegistryRecord[field],$key_id, name, $block_time)
        }
    }
    func action {
        var coords map
        coords = JSONToMap($Coords)
        var coordsAddress, area string
        coordsAddress = coords["address"]
        area = Sprintf("%.0f", coords["area"])

        log("land_use", $LandUse)
        log("buildings_use_class", $BuildingsUseClass)
        log("address", coordsAddress)
        log("map_coords", $Coords)
        log("area", area)

        DBUpdate("land_registry", $LandId, "address,area,buildings_use_class,map_coords,land_use,land_nature,land_local_body", coordsAddress,area,$BuildingsUseClass,$Coords,$LandUse,$LandNature,$LandLocalBody)
    }
}