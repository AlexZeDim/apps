contract land_registry_Add{
    data{
        OwnerId int
        Coords string // {"coords":[{"lat":float,"lng":float},],"area": float,"address": string}
        LandUse int
        BuildingsUseClass int
        RegistrationDate string
        LandNature int
        LandLocalBody int
    }
    
    func conditions{
        var member map
        member = DBFind("members").Where("id = ?", $OwnerId).Row()
        if !member {
            warning "Member account for Owner not found"
        }
        
        $depart2_RoleId = DBFind("roles_list").Where("role_name='land_depart2' and delete=0").One("id")
        $depart3_RoleId = DBFind("roles_list").Where("role_name='land_depart3' and delete=0").One("id")
        if $depart2_RoleId == nil || $depart3_RoleId == nil{
            info "Department roles not found"
        }
        
    }
    func action{
        var icon, header, body, page, params, roleParams, coordsAddress, area string
        var closureType, landId int
        var coords map
        coords = JSONToMap($Coords)
        coordsAddress = coords["address"]
        area = Sprintf("%.0f", coords["area"])
        
        landId = DBInsert("land_registry", "owner_id,address,area,buildings_use_class,map_coords,land_use,timestamp date_create,delete,land_nature,land_local_body,registration_date,timestamp modification_date", $OwnerId, coordsAddress, area, $BuildingsUseClass, $Coords, $LandUse, $block_time, 0, $LandNature, $LandLocalBody, $RegistrationDate, $block_time)
        
        DBInsert("land_registry_ownership", "step, timestamp date_creat, timestamp date_signing, lend_object_id, owner_id, owner_new_id", -1, $block_time, $block_time, landId, $OwnerId, $OwnerId)
        
        icon = "fa fa-bell-o"
        header = "$new_land$"
        body = "$need_processing$"
        closureType = 2
        
        roleParams = "role_id,closure_type,icon_name,text_header,text_body,page_name,params_val"
        
        //notify role2
        params = Sprintf("LandId=%v,depart2=1", landId)
        page = "land_registry_depart2_edit"
        Notifications_Roles_Send(roleParams,Int($depart2_RoleId),closureType,icon,header,body,page,params)
        
        //notify role3
        params = Sprintf("LandId=%v,depart3=1", landId)
        page = "land_registry_depart3_edit"
        Notifications_Roles_Send(roleParams,Int($depart3_RoleId),closureType,icon,header,body,page,params)
        
        //notify owner
        params = Sprintf("LandId=%v,depart1=1", landId)
        page = "land_registry_depart1_edit"
        Notifications_Single_Send("member_id,icon_name,text_header,text_body,page_name,params_val", $OwnerId,icon,header,body,page,params)
    }
}