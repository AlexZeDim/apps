contract lots_Delete {
    data {
        Id int
    }
    func closeNotes(){
        var notes array i lenNotes int note map
        notes = DBFind("notifications").Where("page_params->lot_id=?", $Id).Columns("id")
        lenNotes = Len(notes)
        while i<lenNotes{
            note = notes[i]
            notifications_Close("notific_id", Int(note["id"]))
            i=i+1
        }
    }
    func removeTracks(){
        var tracks array i lenTracks int track map
        tracks = DBFind("lots_tracks").Where("lot_id=?", $Id).Columns("id")
        lenTracks = Len(tracks)
        while i<lenTracks{
            track = tracks[i]
            DBUpdate("lots_tracks", Int(track["id"]), "deleted", 1)
            i=i+1
        }
    }
    conditions {
        var lot map
        lot = DBFind("lots").Where("id=? and deleted=0", $Id).Row()
        if !lot {
            warning "Лот не найден"
        }
        if lot["status"] > 0 && lot["status"] != 5 {
            warning "Вы не можете удалить подтвержденный лот"
        }
        if lot["seller"] != $key_id {
            warning "Вы не являетесь владельцем данного лота"
        }
    }

    action {
        DBUpdate("lots", $Id, "deleted", 1)
        closeNotes()
        removeTracks()
    }
}