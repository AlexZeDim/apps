contract supplychains_supply_add_recipient {
    data {
        cargo_id int

        recipient_id int
        date_dispatch string "date"
        date_receipt string "date"
    }

    conditions {
        $date_dispatch = $date_dispatch + " 00:00"
        $date_receipt = $date_receipt + " 00:00"

        $supply_map = DBFind("supplychains_supply").Where("cargo_id=?", $cargo_id).Row()
        if !$supply_map {
            warning "Supply not found"
        }

        $recipient_map = DBFind("supplychains_recipients").Where("id=?", $recipient_id).Row()
        if !$recipient_map {
            warning "Recipient not found"
        }

        $supplier_map = DBFind("supplychains_suppliers").Where("id=?", $supply_map["supplier_id"]).Row()
        if !$supplier_map {
            warning "Supplier not found"
        }
    }

    action {
        DBUpdate("supplychains_supply", Int($supply_map["id"]), "recipient_id,recipient_name,recipient_role_id,date_dispatch,date_receipt,step,status",
            $recipient_id, Str($recipient_map["name"]), Int($recipient_map["role_id"]), $date_dispatch, $date_receipt, -1, 1)

        DBInsert("supplychains_steps", "supply_id,holder_id,holder_name,holder_type,holder_role_id,step",
            Int($supply_map["id"]), Int($supply_map["supplier_id"]), Str($supply_map["supplier_name"]), 1, Int($supplier_map["role_id"]), -1)

        // add supply in recipient
        $string_supply_id = Str($recipient_map["string_supply_id"])
        $string_supply_id = $string_supply_id + "," + Str(Int($supply_map["id"]))
        DBUpdate("supplychains_recipients", $recipient_id, "string_supply_id", $string_supply_id)
    }
}