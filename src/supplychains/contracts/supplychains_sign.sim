contract supplychains_sign {
    data {
        step_id int
        vFlag int
        photo string "image"
        notific_id int
    }

    conditions {
		$supply_id = DBFind("supplychains_steps").Where("id=?", $step_id).One("supply_id")
        if !$supply_id {
            warning "Step not found"
        }

        $supply_map = DBFind("supplychains_supply").Where("id=?", Int($supply_id)).Row()
        if !$supply_map {
            warning "Supply not found"
        }
    }

    action {

        if ($vFlag == 1) {
            DBUpdate("supplychains_steps", $step_id, "holder_signature, timestamp holder_date_signature", $key_id, $block_time)
        } else {
            DBUpdate("supplychains_steps", $step_id, "receiver_signature, timestamp receiver_date_signature,photo", $key_id, $block_time, $photo)
        }

        $step_map = DBFind("supplychains_steps").Where("id=?", $step_id).Row()
        $holder_signature = Int($step_map["holder_signature"])
        $receiver_signature = Int($step_map["receiver_signature"])

        if ($holder_signature != 0 && $receiver_signature != 0) {

            $step = Int($supply_map["step"])
            $total_steps = Int($supply_map["total_steps"])

            $step = $step + 1

            if ($step >= $total_steps) {
                DBUpdate("supplychains_supply", Int($supply_id), "step,status", $step, 4)
            } else {
                DBUpdate("supplychains_supply", Int($supply_id), "step", $step)
                supplychains_send_notifics("supply_id", Int($supply_id))
            }
        }

        supplychains_cargos_movement("cargo_id,step_id", Int($supply_map["cargo_id"]), $step_id)

        if ($notific_id > 0) {
            Notifications_Single_Close("notific_id", $notific_id)
        }

    }
}