contract TokensEmissionWithdraw {
    data {
        Id int
    }
    conditions {
        MainCondition()
        $item = DBFind("@1tokens").Where({id:$Id}).Row()
        if !$item {
            error "Emission not found"
        }
        $WITH_WITHDRAW = 2
        if $item["type_withdraw"] != $WITH_WITHDRAW {
            error "Emission withdraw error"
        }
        if $item["withdraw"] == 1{
            error "Emission already withdrawn"
        }
        $guest = 4544233900443112470
        $founder = Int(EcosysParam("founder_account"))
    }
    action {
        var amount founderAmount money table date string amounts map amountsDates array i int

        table = Sprintf("@%vkeys", $ecosystem_id)
        founderAmount = Money(DBFind(table).Where({id:$founder, ecosystem:$ecosystem_id}).One("amount"))
        amounts = JSONDecode($item["amount"])
        amountsDates = GetMapKeys(amounts)
        while i < Len(amountsDates){
            date = amountsDates[i]
            amount = amount + Money(amounts[date])
            i = i + 1
        }
        if amount > founderAmount {
            amount = founderAmount
        }
        @1TokensTransfer("Amount,Sender_AccountId,Recipient_AccountId", amount, $founder, $guest)

        DBUpdate("@1tokens", $Id, {withdraw:1})
    }
}