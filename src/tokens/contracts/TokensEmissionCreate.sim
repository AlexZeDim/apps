contract TokensEmissionCreate {
    data {
        Id string "optional"
        Title string
        Amount money
        LimitedDate string "optional"
        LimitedTime string "optional"
        TypeWithdraw int
        TypeEmission int
    }
    func trimZeroTime(s string) string {
        if Contains(s, "T00:00:00Z") {
            s = s Replace(s, "T00:00:00Z", "")
        }
        return s
    }

    func dateAddTime(d, t string) string {
        var dt string
        if Size(t) == 5 {
            dt = Sprintf("%v %v:00", trimZeroTime(d), t)
        }
        return dt
    }

    func fixDatetimes() int{
        var lim int
        lim = UnixDateTime(dateAddTime($LimitedDate, $LimitedTime))

        if lim == 0 { // invalid datetimes
            var errs array
            if Size($LimitedDate) < 10 {
                errs = Append(errs, "date")
            }
            if Size($LimitedTime) < 5 {
                errs = Append(errs, "time")
            }
            error Sprintf(LangRes("@1template_creation_error_x_unspecified", "en"), Join(errs, ", "))
        }
        if lim < $block_time{
            error "Date invalid"
        }
        return lim
    }
    conditions {
        MainCondition()
        $Id = Int($Id)
        if $Id > 0{
            $item = DBFind("@1tokens").Where({id:$Id}).Row()
            if !$item {
                warning "Record not found"
            }
            if $item["type_emission"] == 1 {
                warning "Emission are not editable"
            }
        }
        if Size($Title) == 0{
            error Sprintf(LangRes("@1template_creation_error_x_unspecified", "en"), "title")
        }
        if $Amount <= 0{
            error Sprintf(LangRes("@1template_creation_error_x_unspecified", "en"), "amount")
        }
        if Contains($LimitedDate, "-") {
            $limited = fixDatetimes()
        }else{
            $limited = 0
        }
        $guest = 4544233900443112470
        $founder = Int(EcosysParam("founder_account"))
    }
    action {
        var m amount map
        if $Id > 0{
            m = $item
            amount = JSONDecode($item["amount"])
            if $item["type_emission"] == 2{
                amount[Str($block_time)] = $Amount
            }
            m["amount"] = amount
            if m["type_withdraw"] == 2{
                $item["type_withdraw"] = $TypeWithdraw
            }
            DBUpdate("@1tokens", $Id, m)
        }else{
            amount[Str($block_time)] = $Amount
            m = {type_emission:$TypeEmission, title:$Title, amount:amount, limited:$limited, ecosystem:$ecosystem_id, type_withdraw:$TypeWithdraw}
            $Id = DBInsert("@1tokens", m)
        }
        DBUpdate("@1keys", $founder, {"+amount": $Amount})

        var h map
        h["sender_id"] = 0
        h["recipient_id"] = $founder
        h["amount"] = $Amount
        h["comment"] = Sprintf("emission id %v", $Id)
        h["block_id"] = $block
        h["txhash"] = $txhash
        h["ecosystem"] = $ecosystem_id
        DBInsert("@1history", h)
    }
}