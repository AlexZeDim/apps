contract land_registry_Leasing_SendToken {
    data {
        LandId int
        leasing_id int
        notific_id int
        now_time string "date"
    }

    conditions {
        $land_map = DBFind("land_registry").Where("id = ?", $LandId).Row()
        if !$land_map {
            warning "Land not found"
        }

        $leasing_map = DBFind("land_registry_leasing").Where("id = ?", $leasing_id).Row()
        if !$leasing_map {
            warning "Leasing not found"
        }
    }

    action {

        $sender_map = DBFind("accounts").Where("onhold=$ and member_id=$", 0, Str($leasing_map["member_id"])).Row()
        $recipient_map = DBFind("accounts").Where("onhold=$ and member_id=$", 0, Str($land_map["owner_id"])).Row()
        $sender_amount = DBFind("accounts").Where("id=$", Int($sender_map["id"])).One("amount")

        // переводим залог
        if (  Int($sender_amount) >= Int($land_map["leasing_price"])  ){
            Tokens_Transfer("sender_id,recipient_id,amount", Int($sender_map["id"]), Int($recipient_map["id"]), Money(Int($land_map["leasing_price"])))
            DBInsert("land_registry_history", "sender,recipient,amount,comment,date_create", 
                Int($leasing_map["member_id"]), Int($land_map["owner_id"]), Int($land_map["leasing_price"]), "leasing_sending_deposit", Str($now_time) + (" 00:00"))
        } else {
            warning "Not enough funds on balance"
        }

        // статус договора - заключен. ждем дату начала аренды
        DBUpdate("land_registry_leasing", $leasing_id, "status", 2)

        // проверяем, не настала ли дата арены
        $leasing_map = DBFind("land_registry_leasing").Where("date_start::date <= $ and id=$", $now_time, $leasing_id).Row()
        if $leasing_map {

            $sender_amount2 = DBFind("accounts").Where("id=$", Int($sender_map["id"])).One("amount")
            if (  Int($sender_amount2) >= Int($land_map["leasing_price"])  ){
                Tokens_Transfer("sender_id,recipient_id,amount", Int($sender_map["id"]), Int($recipient_map["id"]), Money(Int($land_map["leasing_price"])))

                DBInsert("land_registry_history", "sender,recipient,amount,comment,date_create", 
                    Int($leasing_map["member_id"]), Int($land_map["owner_id"]), Int($land_map["leasing_price"]), "leasing_payment", Str($now_time) + (" 00:00"))

                //записываем новую дату платежа
                DBUpdate("land_registry_leasing", $leasing_id, "date_payment", $now_time)
                // статус земли - в аренде
                DBUpdate("land_registry", $LandId, "isleasing", $leasing_id)
            } else {
                warning "Not enough funds on balance"
            }
        }

        Notifications_Single_Close("notific_id", $notific_id)
    }
}