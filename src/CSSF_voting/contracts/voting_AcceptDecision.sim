contract voting_AcceptDecision {
    data {
        votingID int
    }

    conditions {
        $voting_map = DBFind("votings").Columns("id,flags->decision,flags->notifics").Where("id=?", $votingID).Row()
        if !$voting_map {
            warning "Voting is not found"
        }

        $subject_map = DBFind("votings_subject").Where("voting_id=$", $votingID).Row()
        if !$subject_map {
            warning "Subject of voting is not found"
        }

        $participant_id = DBFind("votings_participants").Where("voting_id=$ and member->member_id=$", $votingID, $key_id).One("id")
        if !$participant_id {
            warning "You are not a participant in this voting"
        }
    }

    action {
        // if notifications have been sent, then close it
        var noteId string
        noteId = Int(DBFind("notifications").Where("recipient->member_id=? and page_name='voting_view' and page_params->voting_id=? and closed=0", $key_id, $votingID).One("id"))
        if noteId > 0{
            Notifications_Single_Close("notific_id", noteId)
        }

        // check the decision
        if $voting_map["flags.decision"] == 1 {
            warning "Decision was taken. Voting is now not possible"
        }

        // check start-end of voting time
        var voting_start, voting_end string
        voting_start = DBFind("votings").Where("id=$ and date_started < now()", $votingID).One("id")
        voting_end = DBFind("votings").Where("id=$ and date_ended > now()", $votingID).One("id")
        if !voting_start {
            warning "Voting has not yet begun. Try again later, please"
        }
        if !voting_end {
            warning "Voting has expired. Voting is now not possible"
        }

        DBUpdate("votings_participants", Int($participant_id), "timestamp decision_date,decision", $block_time, 1)

        // increment count of accept
        DBUpdate("votings_subject", Int($subject_map["id"]), "+number_accept", 1)

        voting_UpdateDataForGraphs("votingID", $votingID)
    }
}