contract AjamalRoles {
    data {
        rid int
        member_id int
        AjamalCompany int "optional"
        AjamalIdObject string "optional"
        AjamalIdIndividualOwner int "optional"
    }
    
    conditions {
        $role_map = DBFind("roles").Columns("id,deleted,role_type,role_name,image_id,creator->member_id").Where("id = $", $rid).Row()
        if !$role_map {
            warning "Role not found"
        }
        
        $member_map = DBFind("members").Where("id = $", $member_id).Row()
        if !$member_map {
            warning "Member not found"
        }
        
        $appointer_map = DBFind("members").Where("id = $", $key_id).Row()
        if !$appointer_map {
            warning "Appointer not found"
        }
        
        if $role_map["deleted"] == 1 {
            warning "This role has been deleted. The appointment of members is impossible"
        }
        $assign_id = DBFind("roles_participants").Where("role->id=$ and member->member_id=$ and deleted=0", $rid, $member_id).One("id")
        if $assign_id {
            warning "This member has already been added"
        }
        
    }
    
    action {
        var role map
        role["id"] = $role_map["id"]
        role["type"] = $role_map["role_type"]
        role["name"] = $role_map["role_name"]
        role["image_id"] = $role_map["image_id"]
        
        var member map
        member["member_id"] = $member_map["id"]
        member["member_name"] = $member_map["member_name"]
        member["image_id"] = $member_map["image_id"]
        
        var appointed map
        appointed["member_id"] = $appointer_map["id"]
        appointed["member_name"] = $appointer_map["member_name"]
        appointed["image_id"] = $appointer_map["image_id"]
        
        //Tenant Individual check
        if $role_map["role_name"] == "Tenant Individual" {
            DBInsert("roles_participants", "role,member,appointed,timestamp date_created", role, member, appointed, $block_time)
        } 

        //Tenant Company Representative check
        if $role_map["role_name"] == "Tenant Company Representative" {
            $owner_company = DBFind("companies").Where("license_owner_id = $", $AjamalCompany).Row()
            if  $owner_company["license_owner_id"] != $key_id {
                $params_map = `{"ajamal_role_id":"`+$role_map["id"]+`"}`
                Notifications_Single_Send_map("member_id,sender,icon_name,text_header,text_body,page_name,params_map",$owner_company["license_owner_id"],1,"fa-bell","Tenant Company Representative","Need your confirmation","Ajamal_approve_roles",$params_map)
            }
            else { 
                DBInsert("roles_participants", "role,member,appointed,timestamp date_created", role, member, appointed, $block_time)
            }
        }

        //Owner Individual check
        if $role_map["role_name"] == "Owner Individual" {
            if ($AjamalIdObject) {
                $params_map = Sprintf(`{"ajamal_role_id":"`+$role_map["id"]+`",`+`"plot_id":"`+"%v"+`"}`, $AjamalIdObject)
                $individual_map = DBFind("roles").Columns("id,role_name").Where("role_name = $","Manager").Row()
                Notifications_Roles_Send_map("rid,closure_type,sender,icon_name,text_header,text_body,page_name,params_map",$individual_map["id"],1,1,"fa-bell","Owner Individual","Need your confirmation","Ajamal_approve_roles",$params_map)
            }
            else {
                info("Enter Parcel ID/Plot ID")
            }
        }

        //Property manager check
        if $role_map["role_name"] == "Property manager" {
            if ($AjamalIdIndividualOwner) {
                $params_map = `{"ajamal_role_id":"`+$role_map["id"]+`"}`
                Notifications_Single_Send_map("member_id,sender,icon_name,text_header,text_body,page_name,params_map",$AjamalIdIndividualOwner,1,"fa-bell","Property manager","Need your confirmation","Ajamal_approve_roles",$params_map)
            }
            if ($AjamalCompany){
                $params_map = `{"ajamal_role_id":"`+$role_map["id"]+`"}`
                Notifications_Single_Send_map("member_id,sender,icon_name,text_header,text_body,page_name,params_map",$AjamalCompany,1,"fa-bell","Property manager","Need your confirmation","Ajamal_approve_roles",$params_map)
            }
        } 

        //Owner Company
         if $role_map["role_name"] == "Owner Company" {
            if ($AjamalIdObject) {
                $params_map = Sprintf(`{"ajamal_role_id":"`+$role_map["id"]+`",`+`"plot_id":"`+"%v"+`"}`, $AjamalIdObject)
                $individual_map = DBFind("roles").Columns("id,role_name").Where("role_name = $","Manager").Row()
                Notifications_Roles_Send_map("rid,closure_type,sender,icon_name,text_header,text_body,page_name,params_map",$individual_map["id"],1,1,"fa-bell","Owner Individual","Need your confirmation","Ajamal_approve_roles",$params_map)
            }
            else {
                info("Enter Parcel ID/Plot ID")
            }
		}
    }
}