contract Export {
    data {}

    func escapeSpecials(s string) string {
        s = Replace(s, `\`, `\\`)
        s = Replace(s, `	`, `\t`)
        s = Replace(s, "\n", `\n`)
        s = Replace(s, "\r", `\r`)
        s = Replace(s, `"`, `\"`)
        return s
    }

    func AssignAll(items array) string {
        return Sprintf(`{
            "name": "%v",
            "conditions": "%v",
            "data": [
                %v
            ]
        }`, $AppName, $AppConditions, Join(items, ",\r\n"))
    }
    func safeStr(s ...)string{
        // because Str(nil) == "0"
        if s[0] != nil{
            return Str(s[0])
        }
        return ""
    }
    func serializeItem(item map, type string) string {
        var s string
        s = Sprintf(
            `{
                "Type": "%v",
                "Name": "%v",
                "Value": "%v",
                "Conditions": "%v",
                "Menu": "%v",
                "Title": "%v",
                "Trans": "%v",
                "Columns": "%v",
                "Permissions": "%v",
                "Confirmation": "%v"
            }`, type, escapeSpecials(safeStr(item["name"])), escapeSpecials(safeStr(item["value"])), escapeSpecials(safeStr(item["conditions"])), escapeSpecials(safeStr(item["menu"])), escapeSpecials(safeStr(item["title"])), escapeSpecials(safeStr(item["res"])), escapeSpecials(safeStr(item["columns"])), escapeSpecials(safeStr(item["permissions"])), escapeSpecials(safeStr(item["confirmation"]))
        )
        return s
    }

    func getTypeForColumns(table_name string, columnsJSON string) string {
        var colsMap map result columns array
        colsMap = JSONDecode(columnsJSON)
        columns = GetMapKeys(colsMap)
        var i int
        while i < Len(columns){
            if Size(columns[i]) > 0 {
                var col map
                col["name"] = columns[i]
                col["conditions"] = colsMap[col["name"]]
                col["type"] = GetColumnType(table_name, col["name"])
                result = Append(result, col)
            }
            i = i + 1
        }
        return JSONEncode(result)
    }

    func exportTable(type string, result array) array {
        var items array limit offset int
        limit = 250
        while true{
            var rows array
            var where map
            if type == "menu" {
                if Len($menus_names) > 0 {
                    where["name"] = {"$in": $menus_names}
                }
            }else{
                where["app_id"] = $AppID
            }
            if where {
                rows = DBFind(type).Limit(limit).Offset(offset).Where(where)
            }
            if Len(rows) > 0{
                var i int
                while i < Len(rows){
                    items = Append(items, rows[i])
                    i = i + 1
                }
            }else{
                break
            }
            offset = offset + limit
        }
        var i int item map
        while i < Len(items) {
            item = items[i]
            if type == "tables" {
                item["columns"] = getTypeForColumns(item["name"], item["columns"])
            }
            result = Append(result, serializeItem(item, type))
            if type == "pages" {
                $menus_names = Append($menus_names, Sprintf("'%v'", item["menu"]))
            }
            i = i + 1
        }
        return result
    }

    conditions {
        var buf app map
        buf = DBFind("buffer_data").Columns("id,value->app_id,value->app_name").Where({member_id:$key_id, key: "export"}).Row()
        if !buf{
            warning "Application not selected"
        }
        $AppID = Int(buf["value.app_id"])
        $AppName = Str(buf["value.app_name"])

        app = DBFind("applications").Columns("id,conditions").Where({id:$AppID}).Row()
        if !app{
            warning "Application not found"
        }
        $AppConditions = app["conditions"]

        $menus_names = []
    }

    action {
        var exportJSON string, items array
        items = exportTable("pages", items)
        items = exportTable("contracts", items)
        items = exportTable("blocks", items)
        items = exportTable("languages", items)
        items = exportTable("app_params", items)
        items = exportTable("tables", items)
        items = exportTable("menu", items)

        exportJSON = AssignAll(items)
        UploadBinary("Name,Data,ApplicationId,DataMimeType", "export", exportJSON, 1, "application/json")
    }
}