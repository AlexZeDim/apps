contract PoaStore {
    data {
        Id string "optional"
        MemberId int
        NameContract string "optional" // manual catch
        ExpireAt string "optional"
        Params array "optional"
        Values array "optional"
    }
    func dateAddTime(d string) string {
        if Size(d) == 10{
            return d + " 00:00:00"
        }
        return d
    }
    conditions {
        $Id = Int($Id)
        if $Id > 0 {
            if !DBFind("power_of_attorney").Where({id:$Id, "address":$key_id, deleted:0}).One("id") {
                warning "Power of attorney does not exists"
            }
        }
        if !DBFind("keys").WhereId($MemberId).One("id") {
            warning "Attorney not found"
        }
        Println("NameContract",$NameContract)
        Println("ExpireAt",$ExpireAt)
        $ExpireAt = dateAddTime($ExpireAt)
        if GetContractByName($NameContract) == 0{
            warning "Contract for power of attorney not found"
        }
        if Len($Params) > Len($Values) {
            error "Incorrect params list"
        }
    }
    action {
        // amount_max - максимальная сумма для одноразового перевода
        // amount_max_day - максимальная сумма для перевода в день
        // amount_day - переведено в текущий день
        // date_day - учетный день
        // amount_max_month - максимальная сумма для перевода в месяц
        // amount_month переведено в текущий месяц
        // date_month - учетный месяц

        var params map i id lenKeys int
        lenKeys = Len($Params)
        while i < lenKeys {
            params[$Params[i]] = $Values[i]
            i = i + 1
        }
        var m map
        m["key_id_attorney"] = $MemberId
        m["contract"] = $NameContract
        m["date_expiration"] = UnixDateTime($ExpireAt)
        m["params"] = JSONEncode(params)
        if $Id > 0 {
            DBUpdate("power_of_attorney", $Id, m)
        }else{
            m["address"] = $key_id
            m["deleted"] = 0
            id = DBInsert("power_of_attorney", m)

            var n map
            n["member_id"] = $MemberId
            n["sender"] = 1
            n["text_header"] = "You got new power of attorney"
            n["page_name"] = "attorney_add"
            n["params_map"] = Sprintf(`{"id":"%v"}`, id)
            CallContract("NotificationsSend", n)
        }
    }
}