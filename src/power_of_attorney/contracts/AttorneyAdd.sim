contract AttorneyAdd {
    data {
        Id string "optional"
        AttorneyId int
        ContractName string
        DateExpiration string
        ParamsNames array "optional"
        ParamsValues array "optional"
    }
    conditions {
        $Id = Int($Id)
        if $Id > 0 {
            $PowerOfAttorney = DBRow("power_of_attorney").Where("id = ? and address=? and delete=?",$Id,$key_id,0)
            if !$PowerOfAttorney {
                warning "Power of attorney does not exists"
            }
        }
        $Attorney = DBRow("keys").WhereId($AttorneyId)
        if !$Attorney {
            warning "Attorney not found"
        }
        $Contract = DBRow("contracts").Where("name = ?", $ContractName)
        if !$Contract {
            warning "Contract not found"
        }
        if Len($ParamsNames) > Len($ParamsValues) {
            error "Incorrect params list"
        }
    }
    action {
        var params map i,id int
        while i < Len($ParamsNames) {
            params[$ParamsNames[i]] = $ParamsValues[i]
            i = i + 1
        }
        if $Id > 0 {
            DBUpdate("power_of_attorney", $Id, "key_id_attorney,contract,date_expiration,params", $AttorneyId, $ContractName, "timestamp " + $DateExpiration, params)
        }else{
            id = DBInsert("power_of_attorney", "key_id_attorney,address,contract,date_expiration,params", $AttorneyId, $key_id, $ContractName, "timestamp " + $DateExpiration, params)
            var notification map
            notification["member_id"] = $AttorneyId
            notification["sender"] = 1
            notification["text_header"] = "You got new power of attorney"
            notification["page_name"] = "attorney_add"
            notification["params_map"] = Sprintf(`{"id":"%v"}`,id)
            CallContract("notifications_Send",notification)
        }
    }
}