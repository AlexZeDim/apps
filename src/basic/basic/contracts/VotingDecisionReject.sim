contract VotingDecisionReject {
    data {
        votingID int
    }
    
    conditions {
        $voting = DBFind("votings").WhereId($votingID).Columns("voting->type_participants,voting->role_id,flags->decision,flags->notifics").Row()
        if !$voting {
            warning "Voting is not found"
        }
        if $voting["voting.type_participants"] == 3{
            if $voting["voting.role_id"] != $role_id{
                warning "Incorrect role for voting"
            }
        }

        $participant_id = DBFind("votings_participants").Where({voting_id:$votingID, "member->member_id":$key_id}).One("id")
        if !$participant_id {
            warning "You are not a participant in this voting"
        }
    }
    
    action {
        if $voting["flags.notifics"] == 1 {
            // close sent notifications
            var notific_id string
            notific_id = DBFind("notifications").Where({"recipient->member_id":$key_id, page_name:"voting_view", "page_params->voting_id":$votingID, closed:0}).One("id")
            if notific_id {
                NotificationsClose("notific_id", Int(notific_id))
            }
        }
        
        if $voting["flags.decision"] == 1 {
            warning "Decision was taken. Voting is now not possible"
        }
        var voting_start voting_end bt string
        bt = BlockTime()
        voting_start = DBFind("votings").Where({id:$votingID, date_started:[{"$lt":bt}]}).One("id")
        voting_end = DBFind("votings").Where({id:$votingID, date_ended:[{"$gt":bt}]}).One("id")
        if !voting_start {
            warning "Voting has not yet begun. Try again later, please"
        }
        if !voting_end {
            warning "Voting has expired. Voting is now not possible"
        }
        
        
        DBUpdate("votings_participants", Int($participant_id), {decision_date:bt, decision:1})
        
        VotingUpdate("votingID", $votingID)
    }
}