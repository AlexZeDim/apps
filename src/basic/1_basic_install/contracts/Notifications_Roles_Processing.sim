contract Notifications_Roles_Processing {
    data {
        notific_id int
    }

    conditions {
        $notifications_map = DBFind("notifications").Where("id=$", $notific_id).Row()
        if !$notifications_map {
            warning "Notification not found"
        }

        if $notifications_map["date_start_processing"] != "" {
            warning "Sorry, processing of this notification has already begun"
        }

        $participant_id = DBFind("roles_participants").Where("role->id=$ and member->member_id=$ and deleted=0", Int($notifications_map["role_id"]), $key_id).One("id")
        if $participant_id == 0 {
            warning "Sorry, you are not part of the role for which this notification is intended"
        }

        $processing_map = DBFind("members").Where("id = $", $key_id).Row()
        if !$processing_map {
            warning "Member not found"
        }
    }

    action {
        var processing_info map
        processing_info["member_id"] = $processing_map["id"]
        processing_info["member_name"] = $processing_map["member_name"]
        processing_info["image_id"] = $processing_map["image_id"]

        DBUpdate("notifications", $notific_id, "timestamp date_start_processing,processing_info", $block_time, processing_info)
    }
}