contract voting_SubjectCandidates {
    data {
        votingID int
        memberID int
    }
    func safeJsonToMap(m string) map {
        var res map
        if Size(m) > 0 { res = JSONToMap(m) }
        return res
    }
    
    conditions {
        $voting_map = DBFind("votings").Columns("id,date_started,optional,voting->type,flags->full_data").Where("id=? and date_started > now()", $votingID).Row()
        if !$voting_map {
            warning "Voting already started. Can not edit document"
        }
        
        if $voting_map["flags.full_data"] == 1 && $voting_map["voting.type"] == 2 {
            warning "This vote is a system. Change settings not allowed"
        }
        
        if $memberID == 0 {
            $memberID = $key_id
        }
        
        $subject_id = DBFind("votings_subject").Where("voting_id=$ and subject->member_id=$", $votingID, $memberID).One("id")
        if $subject_id {
            warning "This candidature for this role has already been added before"
        }
        
        $member_map = DBFind("members").Where("id = $", $memberID).Row()
        if !$member_map {
            warning "Member not found"
        }
    }
    
    action {
        
        var subject map
        subject["member_id"] = $member_map["id"]
        subject["member_name"] = $member_map["member_name"]
        subject["image_id"] = $member_map["image_id"]
        
        DBInsert("votings_subject", "voting_id,subject", $votingID, subject)
        
        var optional map
        optional = safeJsonToMap($voting_map["optional"])
        
        var number_candidates int
        if optional["number_candidates"]{
            number_candidates = Int(optional["number_candidates"])
            number_candidates = number_candidates + 1
        } else {
            number_candidates = 1
        }
        
        optional["number_candidates"] = number_candidates
        DBUpdate("votings", $votingID, "optional", optional)
        
        //voting_SubjectCheckFulldata("votingID", $votingID)
    }
}