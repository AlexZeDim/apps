contract Notifications_Roles_Send_map {
    data {
        rid int
        closure_type int
        sender int
        icon_name string
        text_header string
        text_body string "optional"
        page_name string
        params_map map
    }
    
    conditions {
        $roles_map = DBFind("roles").Where("id = $", $rid).Row()
        if !$roles_map {
            warning "Role not found"
        }
        
        $participant_id = DBFind("roles_participants").Where("role->id=$ and deleted=0", $rid).One("id")
        if !$participant_id {
            warning "Role does not contain members"
        }
        
        if $sender == 1 {
            $sender_map = DBFind("members").Where("id = $", $key_id).Row()
            if !$sender_map {
                warning "Member not found"
            }
        }
        if $sender == 2 {
            // !!! need to fix role_id to the global variable !!!!
            $sender_map = DBFind("roles").Where("id = $", 1).Row()
            if !$sender_map {
                warning "Member not found"
            }
        }
    }
    
    action {
        //closure_type - Single
        if $closure_type == 1 {
            var recipient map
            recipient["role_id"] = $roles_map["id"]
            recipient["role_name"] = $roles_map["role_name"]
            recipient["image_id"] = $roles_map["image_id"]
            
            var sender map
            if $sender == 1 {
                sender["type"] = 1
                sender["member_id"] = $sender_map["id"]
                sender["member_name"] = $sender_map["member_name"]
                sender["image_id"] = $sender_map["image_id"]
            }
            if $sender == 2 {
                sender["type"] = 2
                sender["role_id"] = $sender_map["id"]
                sender["role_name"] = $sender_map["role_name"]
                sender["image_id"] = $sender_map["image_id"]
            }
            
            var notification map
            notification["header"] = $text_header
            notification["type"] = 2
            notification["icon"] = $icon_name
            notification["body"] = $text_body
            
            DBInsert("notifications", "recipient,sender,notification,page_name,page_params,timestamp date_created",
            recipient, sender, notification, $page_name, $params_map, $block_time)
        }
        //closure_type - Multiple
        else {
            var participants array
            participants = DBFind("roles_participants").Columns("member->member_id").Where("role->id = $ and deleted=0", $rid)
            
            var i int
            while i < Len(participants) {
                var p map
                p = participants[i]
                Notifications_Single_Send_str("member_id,sender,icon_name,text_header,text_body,page_name,params_map",
                Int(p["member.member_id"]), $sender, $icon_name, $text_header, $text_body, $page_name, $params_map)
                
                i = i + 1
            }
        }
    }
}