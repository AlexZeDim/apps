contract Roles_Del {
    data {
        rid int
    }
    
    conditions {        
        var role map
        role = DBFind("roles").Columns("id,role_type,creator->member_id").Where("id=?", $rid).Row()
        if !role {
            warning "Role not found"
        }
        
        if role["role_type"] == 3 {
            warning "System role can not be removed"
        }
        
        if role["creator.member_id"] != $key_id {
            warning "Sorry, you are not the creator of this role"
        }
    }
    
    action {
        var participants array
        participants = DBFind("roles_participants").Columns("id").Where("role->id=? and deleted=0", $rid)
        
        // delete all participants
        var i int
        while i < Len(participants) {
            var p map
            p = participants[i]
            Roles_Unassign("row_id", Int(p["id"]))
            i = i + 1
        }
        
        DBUpdate("roles", $rid, "deleted,timestamp date_deleted", 1, $block_time)
    }
}