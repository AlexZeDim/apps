contract NotificationsClose {
    data {
        notific_id int
        Lang string "optional"
    }

    conditions {
        $notify = DBFind("@1notifications").Where({ecosystem:$ecosystem_id, id:$notific_id}).Columns("id,date_start_processing,processing_info->member_id,closed,notification->type").Row()
        if !$notify {
            warning LangRes("@1notification_not_found", $Lang)
        }
        if $notify["notification.type"] == 2{
            //role has processing check
            if $notify["date_start_processing"] == "" {
                warning LangRes("@1notification_processing_not_started", $Lang)
            }

            if $notify["processing_info.member_id"] != $key_id {
                warning LangRes("@1notice_processing_started_another_member", $Lang)
            }
        }elif $notify["notification.type"] != 1 { // not single
            warning LangRes("@1notification_type_not_found", $Lang)
        }
    }

    action {
        if $notify["closed"] > 0 {
            return
        }
        if $notify["notification.type"] == 1{
            @1NotificationsProcess("notific_id", $notific_id)
        }
        var m map
        m["date_closed"] = BlockTime()
        m["closed"] = 1
        DBUpdate("@1notifications", $notific_id, m)
    }
}