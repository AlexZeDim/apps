contract TokensSend {
    data {
        Amount money
        Recipient_Account string
    }
    func getPermission() {
        var array_permissions array result i int prevContract string
        array_permissions = ["@1TokensSend", "@1TaKeyCheck", "@1TaTransferSeller"]

        prevContract = $stack[0]
        if Len($stack) > 2 {
            prevContract = $stack[Len($stack) - 2]
        }
        while i < Len(array_permissions) {
            var contract_name string
            contract_name = array_permissions[i]
            if contract_name==prevContract {
                result = 1
            }
            i = i + 1
        }

        if result == 0 {
            error LangRes("@1tokens_transfer_cannot_be_made", "en")
        }
    }

    conditions {
        getPermission()

        $recipient = AddressToId($Recipient_Account)
        if $recipient == 0 {
            error Sprintf(LangRes("@1recipient_x_invalid", "en"), $Recipient_Account)
        }
		
        var sender_map map
        sender_map = DBFind("@1keys").Where({ecosystem:$ecosystem_id, id:$key_id}).Row()
        var recipient_map map
        recipient_map = DBFind("@1keys").Where({ecosystem:$ecosystem_id, id:$recipient}).Row()

        //check the sender
        if !sender_map {
            error Sprintf(LangRes("@1sender_x_invalid", "en"), $key_id)
        }
        if Int(sender_map["blocked"]) != 0 {
            error LangRes("@1sender_blocked", "en")
        }
        if Int(sender_map["deleted"]) != 0 {
            error LangRes("@1sender_deleted", "en")
        }

        //check the recipient
        if !recipient_map {
            error Sprintf(LangRes("@1recipient_x_invalid", "en"), $recipient)
        }
        if Int(recipient_map["blocked"]) != 0 {
            error LangRes("@1recipient_blocked", "en")
        }
        if Int(recipient_map["deleted"]) != 0 {
            error LangRes("@1recipient_deleted", "en")
        }
    }

    action {
        @1TokensTransfer("Amount,Sender_AccountId,Recipient_AccountId", $Amount, $key_id, $recipient)
    }
}